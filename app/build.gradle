import rocks.mobileera.buildsrc.Dependencies
import rocks.mobileera.buildsrc.App

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {

    compileSdkVersion 29

    defaultConfig {
        applicationId "rocks.mobileera.rockthegradle"

        minSdkVersion 21
        targetSdkVersion 29

        versionCode App.Version.code
        versionName App.Version.name

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions 'bundle'
    productFlavors {
        free {
            dimension 'bundle'
            applicationId "fr.androidmakers.rockthegradle.free"
        }
        full {
            dimension 'bundle'
        }
    }
}

dependencies {
    implementation Dependencies.Libraries.Kotlin

    implementation Dependencies.Libraries.AndroidX

    api Dependencies.Libraries.Rx

    testImplementation Dependencies.Libraries.JUnit4
}

apply plugin: "getStrings"

getStrings {
    baseUrl = "https://raw.githubusercontent.com/xgouchet/RockTheGradle/strings"

    variants {
        debug {
            languages = ["en"]
        }

        free {
            languages = ["fr"]
        }

        fullRelease {
            languages = ["en", "fr", "es"]
        }
    }
}
